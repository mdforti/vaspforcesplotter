set terminal pdf enhanced color
mioutput='fuerzas.pdf'
bookmarks='bookmarks.txt'
set output mioutput
set print bookmarks
print "#Bookmarks for this case"
set print

NION=system("grep NIONS OUTCAR | awk '{print $NF}'")

set logscale y
set key
set grid ytics
set xlabel " Ciclo Ionico"
set ylabel " Fuerza (eV / ".angstrom.")"
set xtics format "%h"
set ytics format "%h" #"10^{%S}"
set lmargin 15
set bmargin 5
set key outside  bottom

i=0
do for [atom=1:NION] {
atomnum=sprintf("%03d",atom)
atomvar=sprintf("%d",atom)

atsym="Fe"
 if (atom <=56 ) {  atsym="Fe" }
 if ( (atom<=56+12) && (atom>56) ) { atsym = "Fe_{tet}" }
 if ( (atom<=56+12+20 ) && (atom > 56+12) ) {  atsym="Fe_{octa}" }
 if ( atom > 56+12+20 ) { atsym = "O" }
 print atomnum
 input='force'.atomnum
 getdynamics="awk -v ver=".atomvar." '/Direct/ {for(i=1;i<=ver;i++)getline;print $(NF-2) $(NF-1) $NF}' CONTCAR"
 # getdynamics="sed -nr '".atomvar.",".atomvar."{s/\\s+//g;p}' INPUTS/SELECTIVE_3"
 dynamics=system(getdynamics)
 print input
 print getdynamics
 print dynamics
 print "========"
 if (dynamics eq "TTT")||(dynamics eq "FFT") {
  i=i+1
  set print bookmarks append
  print "BookmarkBegin"
  print "BookmarkTitle: Atomo ".atsym.",  ".atomvar
  print "BookmarkLevel: 1"
  print "BookmarkPageNumber: ".sprintf("%d",i)
  set print
  set title 'Fuerzas sobre el '.atsym.' '.atomnum.' Dinamica :'.dynamics
  plot input u  0:(abs($4)) w lp ps 0.5 title 'Fx',\
          '' u 0:(abs($5)) w lp ps 0.5 title 'Fy',\
  	  '' u 0:(abs($6)) w lp ps 0.5 title 'Fz',\
	  '' u 0:(sqrt( abs($4)**2 + abs($5)**2 + abs($6)**2 ) ) w lp ps 0.5 title 'modulo',\
	  1e-1 w l lt 1 dt 2 noti, 5e-2 w l lt 1 dt 2 noti , 1e-2 w l lt 1 dt 2 noti,\
          'borders.dat' u  1:(1e-5):(0):(1) w vectors nohead noti
  } 
} 

set print
system("ls -tral *.pdf")
